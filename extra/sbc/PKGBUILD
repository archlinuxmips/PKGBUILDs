# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

# Arch Linux MIPS:
# - echo "void __builtin_cpu_init(void) {};" >> sbc/sbc_primitives.c

pkgname=sbc
pkgver=1.5
pkgrel=2
pkgdesc="Bluetooth Subband Codec (SBC) library"
url="https://git.kernel.org/pub/scm/bluetooth/sbc.git"
arch=('mips64el')
license=(GPL LGPL)
depends=(glibc)
makedepends=(git)
provides=(libsbc.so)
_commit=b2c20a3ecf5ed00f63c94077a9762b505935ede9  # tags/1.5^0
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
  echo "void __builtin_cpu_init(void) {};" >> sbc/sbc_primitives.c
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --disable-tester
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
