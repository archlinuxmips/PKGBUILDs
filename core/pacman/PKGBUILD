# vim: set ts=2 sw=2 et:
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

# Arch Linux MIPS:
# - treated mips64 as mips64el
# - modified makepkg.conf

pkgname=pacman
pkgver=5.2.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('mips64el')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl'
         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
makedepends=('asciidoc')
checkdepends=('python' 'fakechroot')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
provides=('libalpm.so')
backup=(etc/pacman.conf
        etc/makepkg.conf)
options=('strip' 'debug')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
        pacman.conf
        makepkg.conf
	conf_uname_mips64el.patch
	test_uname_mips64el.patch)
sha256sums=('bb201a9f2fb53c28d011f661d50028efce6eef2c1d2a36728bdd0130189349a0'
            'SKIP'
            '3353f363088c73f1f86a890547c0f87c7473e5caf43bbbc768c2e9a7397f2aa2'
            '5881303dc07237ea1d8d662d29926832fdac9e2959a3c3abd58d372d3801b749'
            '2a93e64c1f109a307cfc2763aeeac3484e86d4c36d0da7cb3f30dc0115630396'
            '5e9347ba2b93f7f6e32a2b24c2d5e5c6bd21f2b31043c08f6a04ca17fc7d6c97')

prepare() {
  # Arch Linux MIPS can only run on mips64el machines,
  # treat mips64 as mips64el
  patch -p0 -i "$srcdir/conf_uname_mips64el.patch"
  patch -p0 -i "$srcdir/test_uname_mips64el.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig
  make V=1
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
}
